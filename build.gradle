/*
 * Copyright 2019 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'maven-publish'
    id 'signing'
}

description = 'Artifact Registry Maven Tools'
ext.project_version = '2.2.1-SNAPSHOT'
ext.isReleaseVersion = !project_version.endsWith("SNAPSHOT")

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    group = 'com.google.cloud.artifactregistry'
    version = project_version
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    repositories {
        jcenter()
    }

    task ensureNoGuavaAndroid {
        doLast {
            configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts.each { artifact ->
                def dependency = artifact.moduleVersion.id
                if (dependency.name == "guava" && dependency.version.contains("android")) {
                    throw new GradleException("unexpected guava android in " + project + ", found '" + dependency +"'");
                }
            }
        }
    }
    compileJava.dependsOn ensureNoGuavaAndroid
}

subprojects {
    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

def commonPomAttributes(pom) {
    pom.url = "https://github.com/GoogleCloudPlatform/build-artifacts-maven-tools"
    pom.developers {
        developer {
            organization= "Google LLC"
            organizationUrl= "http://www.google.com"
        }
    }
    pom.licenses {
        license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }
    pom.scm {
        connection = "scm:git:https://github.com/GoogleCloudPlatform/artifact-registry-maven-tools.git"
        developerConnection = "scm:git:https://github.com/GoogleCloudPlatform/artifact-registry-maven-tools.git"
        url = "https://github.com/GoogleCloudPlatform/artifact-registry-maven-tools"
    }
}

publishing {
    publications {
        wagon(MavenPublication) {
            artifactId = "artifactregistry-maven-wagon"
            def project = project(":artifactregistry-maven-wagon")
            from project.components.java
            artifact(project.sourcesJar) {
                classifier = 'sources'
            }
            artifact(project.javadocJar) {
                classifier = 'javadoc'
            }
            commonPomAttributes(pom)
            pom {
                name = 'Artifact Registry Maven Wagon'
                description = 'A Maven wagon used to connect to Artifact Registry Maven repositories.'
            }
        }
        authCommon(MavenPublication) {
            artifactId = "artifactregistry-auth-common"
            def project = project(":artifactregistry-auth-common")
            from project.components.java
            artifact(project.sourcesJar) {
                classifier = 'sources'
            }
            artifact(project.javadocJar) {
                classifier = 'javadoc'
            }
            commonPomAttributes(pom)
            pom {
                name = 'Artifact Registry common authentication library'
                description = 'Common authentication library for connecting to Artifact Registry.'
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            if (rootProject.hasProperty("ossrhUsername") && rootProject.hasProperty("ossrhPassword")) {
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
}

project(":artifactregistry-maven-wagon") {
    dependencies {
        compile project(":artifactregistry-auth-common")
    }
}

project(":artifactregistry-gradle-plugin") {
    dependencies {
        compile project(":artifactregistry-auth-common")
    }
}

signing {
    sign publishing.publications.wagon
    sign publishing.publications.authCommon
}
